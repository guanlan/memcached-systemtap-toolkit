global begin
global result
global already_processd

probe begin{
	printf("Tracing memcached processing request time cost\n\n")
	printf("Press Ctrl-c to end\n")
}

probe end{
	if(@count(result) > 0)
	{
		printf("\n%d reqs, avg time: %d us\n",@count(result),@avg(result))
		printf("Each req cost time(us):\n")
		print(@hist_linear(result, 0 , 400, 10))
	}
}

probe process("/home/xguru/memcached/memcached").function("drive_machine"){ 
	fd = @var("c")->sfd
	state = @var("c")->state
	#when state is read, parser_cmd, swallow or nread
	if(state == 3 || state == 4 || state == 6|| state == 7)
	{
		begin[fd] = gettimeofday_us()
		already_processd[fd] = 0
	}
		
}

probe process("/home/xguru/memcached/memcached").function("add_iov"){
	fd = @var("c")->sfd
	if(already_processd[fd] != 1){
		result <<< gettimeofday_us() - begin[fd]
		already_processd[fd] = 1
	}
}

